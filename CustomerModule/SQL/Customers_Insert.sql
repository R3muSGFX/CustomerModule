ALTER PROCEDURE [dbo].[Customers_Insert]
	@ERROR_NUMBER INT OUT,
	@ERROR_MESSAGE NVARCHAR(4000) OUT,
	@Customer_ID INT OUT,
	@Customer_Name NVARCHAR(30),
	@Customer_Surname NVARCHAR(30),
	@Customer_Phonenumber NVARCHAR(10),
	@Customer_Address NVARCHAR(50)
AS

	SET TRANSACTION ISOLATION LEVEL READ COMMITTED;
	SET NOCOUNT ON;

	-- Variables
	DECLARE @TRANCOUNT INT = @@TRANCOUNT
	DECLARE @TRANNAME VARCHAR(38) = NEWID()

	-- Initializations
	SET @ERROR_NUMBER = 0
	SET @ERROR_MESSAGE = N''

	-- Begin transaction block
	BEGIN TRY
		IF @TRANCOUNT = 0
			BEGIN TRANSACTION;
		ELSE
			SAVE TRANSACTION @TRANNAME;

		INSERT
			[dbo].[Customers]
			(
				CUSTOMER_NAME,
				CUSTOMER_SURNAME,
				CUSTOMER_PHONENUMBER,
				CUSTOMER_ADDRESS
			)
		VALUES
		(
			@Customer_Name,
			@Customer_Surname,
			@Customer_Phonenumber,
			@Customer_Address
		)

		SET @Customer_ID = SCOPE_IDENTITY()

		-- End transaction block
		IF @TRANCOUNT = 0
			COMMIT TRANSACTION;
	
	END TRY
	BEGIN CATCH
		SET @ERROR_NUMBER = ERROR_NUMBER();
		SET @ERROR_MESSAGE = FORMATMESSAGE('%s:%d - %s', ERROR_PROCEDURE(), ERROR_LINE(), ERROR_MESSAGE());
		IF @TRANCOUNT = 0
			ROLLBACK TRANSACTION;
		ELSE IF XACT_STATE() != -1
			ROLLBACK TRANSACTION @TRANNAME;
		ELSE
			RAISERROR (@ERROR_MESSAGE, 11, 1);
	END CATCH;